# vim: syntax=cmake
if(NOT CMAKE_BUILD_TYPE)
    # default to Release build for GCC builds
    set(CMAKE_BUILD_TYPE Release CACHE STRING
        "Choose the type of build, options are: None(CMAKE_CXX_FLAGS or CMAKE_C_FLAGS used) Debug Release RelWithDebInfo MinSizeRel."
        FORCE)
endif()
message(STATUS "cmake version ${CMAKE_VERSION}")
if(POLICY CMP0025)
    cmake_policy(SET CMP0025 OLD) # report Apple's Clang as just Clang
endif()
if(POLICY CMP0042)
    cmake_policy(SET CMP0042 NEW) # MACOSX_RPATH
endif()
if(POLICY CMP0054)
    cmake_policy(SET CMP0054 OLD) # Only interpret if() arguments as variables or keywords when unquoted
endif()

project (vca)
cmake_minimum_required (VERSION 2.8.8) # OBJECT libraries require 2.8.8
include(CheckIncludeFiles)
include(CheckFunctionExists)
include(CheckSymbolExists)
include(CheckCXXCompilerFlag)

option(FPROFILE_GENERATE "Compile executable to generate usage data" OFF)
option(FPROFILE_USE "Compile executable using generated usage data" OFF)
option(NATIVE_BUILD "Target the build CPU" OFF)
option(STATIC_LINK_CRT "Statically link C runtime for release builds" OFF)
mark_as_advanced(FPROFILE_USE FPROFILE_GENERATE NATIVE_BUILD)
set(VCA_BUILD 1)
configure_file("${PROJECT_SOURCE_DIR}/vca_config.h.in"
               "${PROJECT_BINARY_DIR}/vca_config.h")

SET(CMAKE_MODULE_PATH "${PROJECT_SOURCE_DIR}/cmake" "${CMAKE_MODULE_PATH}")

if(UNIX)
    list(APPEND PLATFORM_LIBS pthread)
    find_library(LIBRT rt)
    if(LIBRT)
        list(APPEND PLATFORM_LIBS rt)
    endif()
    mark_as_advanced(LIBRT)
    find_library(LIBDL dl)
    if(LIBDL)
        list(APPEND PLATFORM_LIBS dl)
    endif()
    option(ENABLE_LIBNUMA "Enable libnuma usage (Linux only)" ON)
    if(ENABLE_LIBNUMA)
        find_package(Numa)
        if(NUMA_FOUND)
            link_directories(${NUMA_LIBRARY_DIR})
            list(APPEND CMAKE_REQUIRED_LIBRARIES numa)
            check_symbol_exists(numa_node_of_cpu numa.h NUMA_V2)
            if(NUMA_V2)
                add_definitions(-DHAVE_LIBNUMA)
                message(STATUS "libnuma found, building with support for NUMA nodes")
                list(APPEND PLATFORM_LIBS numa)
                include_directories(${NUMA_INCLUDE_DIR})
            endif()
        endif()
        mark_as_advanced(NUMA_FOUND)
    endif(ENABLE_LIBNUMA)
    option(NO_ATOMICS "Use a slow mutex to replace atomics" OFF)
    if(NO_ATOMICS)
        add_definitions(-DNO_ATOMICS=1)
    endif(NO_ATOMICS)
endif(UNIX)

if(X64 AND NOT WIN32)
    option(ENABLE_PIC "Enable Position Independent Code" ON)
else()
    option(ENABLE_PIC "Enable Position Independent Code" OFF)
endif(X64 AND NOT WIN32)

# Compiler detection
if(CMAKE_GENERATOR STREQUAL "Xcode")
  set(XCODE 1)
endif()
if(APPLE)
  add_definitions(-DMACOS=1)
endif()

if(${CMAKE_CXX_COMPILER_ID} STREQUAL "Clang")
    set(CLANG 1)
endif()
if(${CMAKE_CXX_COMPILER_ID} STREQUAL "Intel")
    set(INTEL_CXX 1)
endif()
if(${CMAKE_CXX_COMPILER_ID} STREQUAL "GNU")
    set(GCC 1)
endif()

if(INTEL_CXX AND WIN32)
    # treat icl roughly like MSVC
    set(MSVC 1)
endif()
if(MSVC)
    if(STATIC_LINK_CRT)
        set(CompilerFlags CMAKE_CXX_FLAGS_RELEASE CMAKE_C_FLAGS_RELEASE)
        foreach(CompilerFlag ${CompilerFlags})
            string(REPLACE "/MD" "/MT" ${CompilerFlag} "${${CompilerFlag}}")
        endforeach()
    endif(STATIC_LINK_CRT)
    add_definitions(/W4)  # Full warnings
    add_definitions(/Ob2) # always inline
    add_definitions(/MP)  # multithreaded build

    # disable Microsofts suggestions for proprietary secure APIs
    add_definitions(/D_CRT_SECURE_NO_WARNINGS)

    check_include_files(stdint.h HAVE_STDINT_H)
    if(NOT HAVE_STDINT_H)
        include_directories(compat/msvc)
    endif()
endif(MSVC)

check_include_files(inttypes.h HAVE_INT_TYPES_H)
if(HAVE_INT_TYPES_H)
    add_definitions(-DHAVE_INT_TYPES_H=1)
endif()

if(INTEL_CXX AND UNIX)
    set(GCC 1) # treat icpc roughly like gcc
elseif(CLANG)
    set(GCC 1) # treat clang roughly like gcc
elseif(CMAKE_COMPILER_IS_GNUCXX)
    set(GCC 1)
endif()

if(CC STREQUAL "xlc")
    message(STATUS "Use XLC compiler")
    set(XLC 1)
    set(GCC 0)
    #set(CMAKE_C_COMPILER "/usr/bin/xlc")
    #set(CMAKE_CXX_COMPILER "/usr/bin/xlc++")
    add_definitions(-D__XLC__=1)
    add_definitions(-O3 -qstrict -qhot -qaltivec)
    add_definitions(-qinline=level=10 -qpath=IL:/data/video_files/latest.tpo/)
endif()
if(WIN32 AND (MSVC_VERSION GREATER 1800))
    if(CMAKE_VERSION VERSION_LESS 3.7)
        message(FATAL_ERROR "cmake version not compatible for VS 2017. Update the cmake to versions 3.7 or above")
    endif()
endif()
if(GCC)
    add_definitions(-Wall -Wextra -Wshadow)
    add_definitions(-D__STDC_LIMIT_MACROS=1)
    if(NOT INTEL_CXX AND NOT CLANG AND CMAKE_CXX_COMPILER_VERSION VERSION_GREATER 8.0)
        add_definitions(-Wno-class-memaccess)
    endif()
    if(ENABLE_HDR10_PLUS)
        if(CMAKE_CXX_COMPILER_VERSION VERSION_LESS "4.8")
            message(FATAL_ERROR "gcc version above 4.8 required to support hdr10plus")
        endif()
        add_definitions(-std=gnu++11)
    else()
        add_definitions(-std=gnu++98)
    endif()
    if(ENABLE_PIC)
         add_definitions(-fPIC)
    endif(ENABLE_PIC)
    if(NATIVE_BUILD)
        if(INTEL_CXX)
            add_definitions(-xhost)
        else()
            add_definitions(-march=native)
        endif()
    elseif(X86 AND NOT X64)
        string(FIND "${CMAKE_CXX_FLAGS}" "-march" marchPos)
        if(marchPos LESS "0")
            add_definitions(-march=i686)
            if(WIN32 AND NOT INTEL_CXX AND NOT CLANG AND
               CMAKE_CXX_COMPILER_VERSION VERSION_GREATER 6.0 AND CMAKE_CXX_COMPILER_VERSION VERSION_LESS 7.0)
                add_definitions(-mpreferred-stack-boundary=2)
            endif()
        endif()
    endif()
    if(ARM AND CROSS_COMPILE_ARM)
        set(ARM_ARGS -march=armv6 -mfloat-abi=soft -mfpu=vfp -marm -fPIC)
    elseif(ARM)
        find_package(Neon)
        if(CPU_HAS_NEON)
            set(ARM_ARGS -mcpu=native -mfloat-abi=hard -mfpu=neon -marm -fPIC)
            add_definitions(-DHAVE_NEON)
        else()
            set(ARM_ARGS -mcpu=native -mfloat-abi=hard -mfpu=vfp -marm)
        endif()
    endif()
    add_definitions(${ARM_ARGS})
    if(FPROFILE_GENERATE)
        if(INTEL_CXX)
            add_definitions(-prof-gen -prof-dir="${CMAKE_CURRENT_BINARY_DIR}")
            list(APPEND LINKER_OPTIONS "-prof-gen")
        else()
            check_cxx_compiler_flag(-fprofile-generate CC_HAS_PROFILE_GENERATE)
            if(CC_HAS_PROFILE_GENERATE)
                add_definitions(-fprofile-generate)
                list(APPEND LINKER_OPTIONS "-fprofile-generate")
            endif(CC_HAS_PROFILE_GENERATE)
        endif(INTEL_CXX)
    endif(FPROFILE_GENERATE)
    if(FPROFILE_USE)
        if(INTEL_CXX)
            add_definitions(-prof-use -prof-dir="${CMAKE_CURRENT_BINARY_DIR}")
            list(APPEND LINKER_OPTIONS "-prof-use")
        else()
            check_cxx_compiler_flag(-fprofile-use CC_HAS_PROFILE_USE)
            check_cxx_compiler_flag(-fprofile-correction CC_HAS_PROFILE_CORRECTION)
            check_cxx_compiler_flag(-Wno-error=coverage-mismatch CC_HAS_COVMISMATCH)
            if(CC_HAS_PROFILE_USE)
                add_definitions(-fprofile-use)
                list(APPEND LINKER_OPTIONS "-fprofile-use")
            endif(CC_HAS_PROFILE_USE)
            if(CC_HAS_PROFILE_CORRECTION)
                # auto-correct corrupted counters
                add_definitions(-fprofile-correction)
            endif(CC_HAS_PROFILE_CORRECTION)
            if(CC_HAS_COVMISMATCH)
                # ignore coverage mismatches
                add_definitions(-Wno-error=coverage-mismatch)
            endif(CC_HAS_COVMISMATCH)
        endif(INTEL_CXX)
    endif(FPROFILE_USE)
    if(STATIC_LINK_CRT)
        add_definitions(-static)
        list(APPEND LINKER_OPTIONS "-static-libgcc")
    endif(STATIC_LINK_CRT)
    check_cxx_compiler_flag(-Wno-strict-overflow CC_HAS_NO_STRICT_OVERFLOW)
    check_cxx_compiler_flag(-Wno-narrowing CC_HAS_NO_NARROWING) 
    check_cxx_compiler_flag(-Wno-array-bounds CC_HAS_NO_ARRAY_BOUNDS) 
    if (CC_HAS_NO_ARRAY_BOUNDS)
        add_definitions(-Wno-array-bounds) # these are unhelpful
    endif()
    check_cxx_compiler_flag(-ffast-math CC_HAS_FAST_MATH) 
    if (CC_HAS_FAST_MATH)
        add_definitions(-ffast-math)
    endif()
    check_cxx_compiler_flag(-mstackrealign CC_HAS_STACK_REALIGN) 
    if (CC_HAS_STACK_REALIGN)
        add_definitions(-mstackrealign)
    endif()
    # Disable exceptions. Reduce executable size, increase compability.
    check_cxx_compiler_flag(-fno-exceptions CC_HAS_FNO_EXCEPTIONS_FLAG)
    if(CC_HAS_FNO_EXCEPTIONS_FLAG)
        add_definitions(-fno-exceptions)
    endif()
    set(FSANITIZE "" CACHE STRING "-fsanitize options for GCC/clang")
    if(FSANITIZE)
        add_definitions(-fsanitize=${FSANITIZE})
        # clang and gcc need the sanitize options to be passed at link
        # time so the appropriate ASAN/TSAN runtime libraries can be
        # linked.
        list(APPEND LINKER_OPTIONS "-fsanitize=${FSANITIZE}")
    endif()
    option(ENABLE_AGGRESSIVE_CHECKS "Enable stack protection and -ftrapv" OFF)
    if(ENABLE_AGGRESSIVE_CHECKS)
        # use with care, -ftrapv can cause testbench SIGILL exceptions
        # since it is testing corner cases of signed integer math
        add_definitions(-DUSING_FTRAPV=1)
        check_cxx_compiler_flag(-fsanitize=undefined-trap CC_HAS_CATCH_UNDEFINED) # clang
        check_cxx_compiler_flag(-ftrapv CC_HAS_FTRAPV)                            # gcc
        check_cxx_compiler_flag(-fstack-protector-all CC_HAS_STACK_PROTECT)       # gcc
        if(CC_HAS_FTRAPV)
            add_definitions(-ftrapv)
        endif()
        if(CC_HAS_CATCH_UNDEFINED)
            add_definitions(-fsanitize=undefined-trap -fsanitize-undefined-trap-on-error)
        endif()
        if(CC_HAS_STACK_PROTECT)
            add_definitions(-fstack-protector-all)
            if(MINGW)
                list(APPEND PLATFORM_LIBS ssp)
            endif()
        endif()
    endif(ENABLE_AGGRESSIVE_CHECKS)
    execute_process(COMMAND ${CMAKE_CXX_COMPILER} -dumpversion OUTPUT_VARIABLE CC_VERSION)
endif(GCC)

option(CHECKED_BUILD "Enable run-time sanity checks (debugging)" OFF)
if(CHECKED_BUILD)
    add_definitions(-DCHECKED_BUILD=1)
endif()

# Build options
set(LIB_INSTALL_DIR lib CACHE STRING "Install location of libraries")
set(BIN_INSTALL_DIR bin CACHE STRING "Install location of executables")

if(HIGH_BIT_DEPTH)
    option(MAIN12 "Support Main12 instead of Main10" OFF)
    if(MAIN12)
        add_definitions(-DHIGH_BIT_DEPTH=1 -DVCA_DEPTH=12)
    else()
        add_definitions(-DHIGH_BIT_DEPTH=1 -DVCA_DEPTH=10)
    endif()
else(HIGH_BIT_DEPTH)
    add_definitions(-DHIGH_BIT_DEPTH=0 -DVCA_DEPTH=8)
endif(HIGH_BIT_DEPTH)

set(VCA_NS vca)
add_definitions(-DVCA_NS=${VCA_NS})

option(WARNINGS_AS_ERRORS "Stop compiles on first warning" OFF)
if(WARNINGS_AS_ERRORS)
    if(GCC)
        add_definitions(-Werror)
    elseif(MSVC)
        add_definitions(/WX)
    endif()
endif(WARNINGS_AS_ERRORS)

if(WIN32)
    # Visual leak detector
    find_package(VLD QUIET)
    if(VLD_FOUND)
        add_definitions(-DHAVE_VLD)
        include_directories(${VLD_INCLUDE_DIRS})
        list(APPEND PLATFORM_LIBS ${VLD_LIBRARIES})
        link_directories(${VLD_LIBRARY_DIRS})
    endif()
    option(WINXP_SUPPORT "Make binaries compatible with Windows XP and Vista" OFF)
    if(WINXP_SUPPORT)
        # force use of workarounds for CONDITION_VARIABLE and atomic
        # intrinsics introduced after XP
        add_definitions(-D_WIN32_WINNT=_WIN32_WINNT_WINXP -D_WIN32_WINNT_WIN7=0x0601)
    else(WINXP_SUPPORT)
        # default to targeting Windows 7 for the NUMA APIs
        add_definitions(-D_WIN32_WINNT=_WIN32_WINNT_WIN7)
    endif(WINXP_SUPPORT)
endif()

include(version) # determine VCA_VERSION and VCA_LATEST_TAG
include_directories(. common encoder "${PROJECT_BINARY_DIR}")

option(ENABLE_VTUNE "Enable Vtune profiling instrumentation" OFF)
if(ENABLE_VTUNE)
    find_package(Vtune)
    if(VTUNE_FOUND)
        add_definitions(-DENABLE_VTUNE)
        include_directories(${VTUNE_INCLUDE_DIR})
        list(APPEND PLATFORM_LIBS vtune)
        link_directories(${VTUNE_LIBRARY_DIR})
        if(WIN32)
            list(APPEND PLATFORM_LIBS libittnotify.lib)
        else()
            list(APPEND PLATFORM_LIBS libittnotify.a dl)
        endif()
        add_subdirectory(profile/vtune)
    endif(VTUNE_FOUND)
endif(ENABLE_VTUNE)

add_subdirectory(encoder)
add_subdirectory(common)

add_library(vca-static STATIC $<TARGET_OBJECTS:encoder> $<TARGET_OBJECTS:common>)

if(NOT MSVC)
    set_target_properties(vca-static PROPERTIES OUTPUT_NAME vca)
endif()
if(EXTRA_LIB)
    target_link_libraries(vca-static ${EXTRA_LIB})
endif()
install(TARGETS vca-static
    LIBRARY DESTINATION ${LIB_INSTALL_DIR}
    ARCHIVE DESTINATION ${LIB_INSTALL_DIR})

install(FILES vca.h "${PROJECT_BINARY_DIR}/vca_config.h" DESTINATION include)
if((WIN32 AND ENABLE_CLI) OR (WIN32 AND ENABLE_SHARED))
    if(MSVC_IDE)
        install(FILES "${PROJECT_BINARY_DIR}/Debug/vca.pdb" DESTINATION ${BIN_INSTALL_DIR} CONFIGURATIONS Debug)
        install(FILES "${PROJECT_BINARY_DIR}/RelWithDebInfo/vca.pdb" DESTINATION ${BIN_INSTALL_DIR} CONFIGURATIONS RelWithDebInfo)
        install(FILES "${PROJECT_BINARY_DIR}/Debug/libvca.pdb" DESTINATION ${BIN_INSTALL_DIR} CONFIGURATIONS Debug OPTIONAL NAMELINK_ONLY)
        install(FILES "${PROJECT_BINARY_DIR}/RelWithDebInfo/libvca.pdb" DESTINATION ${BIN_INSTALL_DIR} CONFIGURATIONS RelWithDebInfo OPTIONAL NAMELINK_ONLY)
    else()
        install(FILES "${PROJECT_BINARY_DIR}/vca.pdb" DESTINATION ${BIN_INSTALL_DIR} CONFIGURATIONS Debug)
        install(FILES "${PROJECT_BINARY_DIR}/vca.pdb" DESTINATION ${BIN_INSTALL_DIR} CONFIGURATIONS RelWithDebInfo)
        install(FILES "${PROJECT_BINARY_DIR}/libvca.pdb" DESTINATION ${BIN_INSTALL_DIR} CONFIGURATIONS Debug OPTIONAL NAMELINK_ONLY)
        install(FILES "${PROJECT_BINARY_DIR}/libvca.pdb" DESTINATION ${BIN_INSTALL_DIR} CONFIGURATIONS RelWithDebInfo OPTIONAL NAMELINK_ONLY)
    endif()
endif()
if(CMAKE_RC_COMPILER)
    # The resource compiler does not need CFLAGS or macro defines. It
    # often breaks them
    string(REPLACE "<FLAGS>" "" CMAKE_RC_COMPILE_OBJECT "${CMAKE_RC_COMPILE_OBJECT}")
    string(REPLACE "<DEFINES>" "" CMAKE_RC_COMPILE_OBJECT "${CMAKE_RC_COMPILE_OBJECT}")
    # convert VCA_LATEST_TAG (ex: 0.7) and VCA_TAG_DISTANCE (ex: 103) to
    # @VCA_VERSION_MAJOR@,@VCA_VERSION_MINOR@,@VCA_BRANCH_ID@,@VCA_TAG_DISTANCE@
    string(REGEX MATCHALL "([0-9]+)" VERSION_LIST "${VCA_LATEST_TAG}")
    list(GET VERSION_LIST 0 VCA_VERSION_MAJOR)
    list(GET VERSION_LIST 1 VCA_VERSION_MINOR)
    set(VCA_BRANCH_ID 0) # TODO: 0 - stable, 1 - default or other
    set(VCA_RC_FILE "${CMAKE_CURRENT_BINARY_DIR}/vca.rc")
    configure_file("${CMAKE_CURRENT_SOURCE_DIR}/vca.rc.in" "${VCA_RC_FILE}" @ONLY)
endif()

if(NOT (MSVC_IDE OR XCODE))
    add_custom_target(clean-generated COMMAND ${CMAKE_COMMAND} -P ${CMAKE_CURRENT_SOURCE_DIR}/cmake/clean-generated.cmake)
endif()
option(ENABLE_SHARED "Build shared library" ON)
if(ENABLE_SHARED)
    add_library(vca-shared SHARED
                ${VCA_RC_FILE} $<TARGET_OBJECTS:encoder> $<TARGET_OBJECTS:common>)
    if(EXTRA_LIB)
        target_link_libraries(vca-shared ${EXTRA_LIB})
    endif()
    target_link_libraries(vca-shared ${PLATFORM_LIBS})
    if(MSVC)
        set_target_properties(vca-shared PROPERTIES OUTPUT_NAME libvca)
    else()
        set_target_properties(vca-shared PROPERTIES OUTPUT_NAME vca)
    endif()
    if(UNIX)
        set_target_properties(vca-shared PROPERTIES VERSION ${VCA_BUILD})
        if(APPLE)
            set_target_properties(vca-shared PROPERTIES MACOSX_RPATH 1)
        elseif(CYGWIN)
            # Cygwin is not officially supported or tested. MinGW with msys is recommended.
        else()
            list(APPEND LINKER_OPTIONS "-Wl,-Bsymbolic,-znoexecstack")
        endif()
    endif()
    set_target_properties(vca-shared PROPERTIES SOVERSION ${VCA_BUILD})
    if(VCA_LATEST_TAG)
        if(WINDOWS)
            set_target_properties(vca-shared PROPERTIES VERSION ${VCA_LATEST_TAG})
        endif()
        # shared library is not installed if a tag is not found
        install(TARGETS vca-shared
                LIBRARY DESTINATION ${LIB_INSTALL_DIR}
                ARCHIVE DESTINATION ${LIB_INSTALL_DIR}
                RUNTIME DESTINATION ${BIN_INSTALL_DIR})
    endif()
    if(LINKER_OPTIONS)
        # set_target_properties can't do list expansion
        string(REPLACE ";" " " LINKER_OPTION_STR "${LINKER_OPTIONS}")
        set_target_properties(vca-shared PROPERTIES LINK_FLAGS "${LINKER_OPTION_STR}")
    endif()
endif()

if(VCA_LATEST_TAG)
    # convert lists of link libraries into -lstdc++ -lm etc..
    foreach(LIB ${CMAKE_CXX_IMPLICIT_LINK_LIBRARIES} ${PLATFORM_LIBS})
        if(IS_ABSOLUTE ${LIB} AND EXISTS ${LIB})
            list(APPEND PLIBLIST "${LIB}")
        else()
            list(APPEND PLIBLIST "-l${LIB}")
        endif()
    endforeach()
    if(PLIBLIST)
        # blacklist of libraries that should not be in Libs.private
        list(REMOVE_ITEM PLIBLIST "-lc" "-lpthread" "-lmingwex" "-lmingwthrd"
            "-lmingw32" "-lmoldname" "-lmsvcrt" "-ladvapi32" "-lshell32"
            "-luser32" "-lkernel32")
        string(REPLACE ";" " " PRIVATE_LIBS "${PLIBLIST}")
    else()
        set(PRIVATE_LIBS "")
    endif(PLIBLIST)

    # Produce a pkg-config file
    configure_file("vca.pc.in" "vca.pc" @ONLY)
    install(FILES       "${CMAKE_CURRENT_BINARY_DIR}/vca.pc"
            DESTINATION "${LIB_INSTALL_DIR}/pkgconfig")
endif()

configure_file("${CMAKE_CURRENT_SOURCE_DIR}/cmake/cmake_uninstall.cmake.in"
               "${CMAKE_CURRENT_BINARY_DIR}/cmake/cmake_uninstall.cmake"
               IMMEDIATE @ONLY)
add_custom_target(uninstall
                  "${CMAKE_COMMAND}" -P "${CMAKE_CURRENT_BINARY_DIR}/cmake/cmake_uninstall.cmake")

# Main CLI application
set(ENABLE_CLI ON CACHE BOOL "Build standalone CLI application")
if(ENABLE_CLI)
    file(GLOB InputFiles input/input.cpp input/yuv.cpp input/y4m.cpp input/*.h)
    source_group(input FILES ${InputFiles})

    check_include_files(getopt.h HAVE_GETOPT_H)
    if(NOT HAVE_GETOPT_H)
        if(MSVC)
            set_source_files_properties(compat/getopt/getopt.c PROPERTIES COMPILE_FLAGS "/wd4100 /wd4131 -DHAVE_STRING_H=1")
        endif(MSVC)
        include_directories(compat/getopt)
        set(GETOPT compat/getopt/getopt.c compat/getopt/getopt.h)
    endif(NOT HAVE_GETOPT_H)
    if(XCODE)
        # Xcode seems unable to link the CLI with libs, so link as one target
        add_executable(cli ../COPYING ${InputFiles} ${GETOPT}
                    vca.cpp vca.h vcacli.h
                    $<TARGET_OBJECTS:encoder> $<TARGET_OBJECTS:common>)
    else()
        add_executable(cli ../COPYING ${InputFiles} ${GETOPT} ${VCA_RC_FILE}
                       vca.cpp vca.h vcacli.h)
        if(WIN32 OR NOT ENABLE_SHARED OR INTEL_CXX)
            # The CLI cannot link to the shared library on Windows, it
            # requires internal APIs not exported from the DLL
            target_link_libraries(cli vca-static ${PLATFORM_LIBS})
        else()
            target_link_libraries(cli vca-shared ${PLATFORM_LIBS})
        endif()
    endif()
    set_target_properties(cli PROPERTIES OUTPUT_NAME vca)
    if(LINKER_OPTIONS)
        # set_target_properties can't do list expansion
        string(REPLACE ";" " " LINKER_OPTION_STR "${LINKER_OPTIONS}")
        set_target_properties(cli PROPERTIES LINK_FLAGS "${LINKER_OPTION_STR}")
    endif()

    install(TARGETS cli DESTINATION ${BIN_INSTALL_DIR})
endif(ENABLE_CLI)

get_directory_property(hasParent PARENT_DIRECTORY)
if(hasParent)
    if(PLATFORM_LIBS)
        LIST(REMOVE_DUPLICATES PLATFORM_LIBS)
        set(PLATFORM_LIBS ${PLATFORM_LIBS} PARENT_SCOPE)
    endif(PLATFORM_LIBS)
endif(hasParent)
